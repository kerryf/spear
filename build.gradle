plugins {
    id 'java'
    id 'groovy'
    id 'application'
}

group = 'ca.kerryf'
version = '1.0'
sourceCompatibility = '21'
targetCompatibility = '21'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.javalin:javalin:6.6.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
    implementation 'org.slf4j:slf4j-jdk14:2.0.17'
    implementation 'org.xerial:sqlite-jdbc:3.49.1.0'
    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'org.apache.groovy:groovy-all:4.0.18'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
    testImplementation 'org.spockframework:spock-core:2.4-M1-groovy-4.0'
}

application {
    mainClass = 'spear.Spear'
}

// Configure Groovy source directories
sourceSets {
    main {
        groovy {
            srcDirs = ['src/main/groovy']
        }
    }
    test {
        groovy {
            srcDirs = ['src/test/groovy']
        }
    }
}

// Configure test task to use JUnit Platform
test {
    useJUnitPlatform()
}

jar {
    archiveFileName = 'spear.jar'
    manifest {
        attributes(
            'Main-Class': 'spear.Spear',
            'Class-Path': configurations.runtimeClasspath.files.collect { 'dependency-jars/' + it.name }.join(' ')
        )
    }
}

// Copy dependencies to dependency-jars directory
task copyDependencies(type: Copy) {
    from configurations.runtimeClasspath
    into "${buildDir}/dependency-jars"
}

build.dependsOn copyDependencies
